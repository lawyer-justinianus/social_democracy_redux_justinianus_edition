title: Economic Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: economic_policy_timer += 12; month_actions += 1; synthesis_budget = 3; synthesis_budget -= 1 if economic_democracy+nationalization_progress >= 3; synthesis_budget -= 1 if synthesis_progress >= 1; nationalize_budget = 4; nationalize_budget -= 1 if works_councils >= 2; nationalize_budget -= 1 if works_councils >= 3; nationalize_budget -= 1 if socializations + nationalization_progress >= 2; wtb_budget = 4; wtb_budget -= 1 if works_councils >= 2 or socializations >= 1 or pro_labor >= 2
view-if: spd_in_government = 1 and (economic_minister_party == "SPD" or finance_minister_party == "SPD") and economic_policy_timer <= 0 and economic_plan > 0
card-image: img/portraits/SchmidtRobert.jpg
#subtitle: Policies to address the economic crisis.

= Economic Policy

We have already formulated an economic plan. Now we have to implement it.

- @wtb_2
- @wtb_2_deficit
- @wtb_continuation
- @implement_wtb_no_deficit
- @implement_wtb_deficit
- @nationalize_1
- @nationalize_2
- @moderate_plan
- @moderate_plan_limited_budget
- @synthesis_plan
- @synthesis_only_works
- @root: Do not implement the economic plan.
- @cancel_advisor_action
- @easy_discard

@wtb_2
title: Continue the implementation of the WTB-Plan.
subtitle: -3 budget
unavailable-subtitle: [? if unemployed <= 8 : Unemployment is low enough that the time for the WTB-Plan has passed. ?][? if economic_minister_party == "DVP" : The DVP-controlled Economic Ministry is obstructing our plans. ?]
view-if: wtb_implemented > 2 and budget >= 3 
choose-if: unemployed > 8 and economic_minister_party != "DVP"
on-arrival: works_program += 1; budget -= 3; workers_spd += 7*(1-dissent); unemployed -= 1 if works_program <= 4; workers += 1 if works_program <= 4; workers += 3 if unemployed > 8; unemployed -= 3 if unemployed > 8; unemployed_spd += 7*(1-dissent); workers_kpd -= 3; workers_nsdap -= 3; workers_dnvp -= 3; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; economic_growth += 1.6; coalition_dissent += 1 if in_grand_coalition; economic_growth -= 0.6 if economic_growth > 5; socialism += 3

[? if works_program == 4 : We have commenced Phase 2 of the WTB-Plan. ?][? if works_program > 4 :  We have continued our implementation of the WTB-Plan. ?] In addition to public works, we are now focusing on rebuilding our industries.


@wtb_2_deficit
title: Continue the implementation of the WTB-Plan.
subtitle: -3 budget - we will enter a deficit.
unavailable-subtitle: [? if unemployed <= 10 : Unemployment is low enough that we should not do deficit spending. ?][? if economic_minister_party == "DVP" : The DVP-controlled Economic Ministry is obstructing our plans. ?][? if finance_minister_party != "SPD" : Without control over finances, we cannot undertake such deficit spending. ?]
view-if: wtb_implemented > 2 and budget < 3 and budget >= -3
choose-if: unemployed > 10 and economic_minister_party != "DVP" and finance_minister_party == "SPD"
on-arrival: works_program += 1; budget -= 3; workers_spd += 6*(1-dissent); unemployed -= 1 if works_program <= 4; workers += 1 if works_program <= 4; workers += 3 if unemployed > 8; unemployed -= 3 if unemployed > 8; unemployed_spd += 6*(1-dissent); workers_kpd -= 2; workers_nsdap -= 2; workers_dnvp -= 2; coalition_dissent += 1; coalition_dissent += 1 if in_grand_coalition; capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; economic_growth += 1.6; economic_growth -= 0.6 if economic_growth > 5; socialism += 2

We have continued our implementation of the WTB-Plan, despite the continued deficit spending. In addition to public works, we are now focusing on rebuilding our industries.


@wtb_continuation
title: Look back on our results, and continue our implementation...
view-if: wtb_implemented >= 1 and wtb_implemented <= 2
on-arrival: budget += 1 if works_program <= 2; workers += 5 if (works_program <= 2 and unemployed > 15) else workers += 3; unemployed -= 5 if (works_program <= 2 and unemployed > 15) else unemployed -= 3; unemployed += 3 if unemployed < 5; unemployed += 3 if unemployed < 0; workers += 2 if works_program <= 2 else workers += 1; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); workers_kpd -= 3; workers_nsdap -= 5; workers_dnvp -= 5; works_program += 1; z_relation += 3; coalition_dissent += 1 if in_grand_coalition; coalition_dissent -= 1 if coalition_dissent >= 1; wtb_implemented += 1; capital_strike_progress -= 1 if capital_strike_progress >= 3; inflation += 1.5; inflation += 1 if unemployed < 10; economic_growth += 1.2; economic_growth -= 0.5 if economic_growth > 5; socialism += 1

Our spending on public works has resulted in a lower need for unemployment insurance and welfare, and increased tax receipts through the multiplier effect as the new money courses through the economy, recuperating some of our spending. We continue to invest most of the new revenue into the program.

The economy is doing better, finally. Even the bourgeois parties are beginning to see the light[? if grand_coalition :, though the DVP continues to grumble about government intrusion into the economy ?].

[? if in_popular_front or in_left_front: The KPD, however, still opposes our policy of "rescuing" capitalism. ?]

@implement_wtb_no_deficit
title: Implement the WTB plan.
subtitle: -[+ wtb_budget +] budget. [? if wtb_budget == 3 : Our pro-labor policies have reduced the cost of the plan. ?]
unavailable-subtitle: [? if unemployed <= 8 : Unemployment is low enough that the time for the WTB-Plan has passed. ?][? if economic_minister_party == "DVP" : The DVP-controlled Economic Ministry is obstructing our plans. ?]
view-if: budget >= wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8 and economic_minister_party != "DVP"
on-arrival: wtb_no_deficit_vote = 1
go-to: wtb_no_deficit_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to implement the WTB plan.

@wtb_no_deficit_done
on-arrival: budget -= wtb_budget; works_program += 1; workers += 4; unemployed -= 4; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; economic_growth += 2.8; coalition_dissent += 1 if in_grand_coalition; pro_republic += 3*(1-dissent); socialism += 5
achievement: wirtschaftspolitik

We have started to enact the WTB plan. Money is beginning to flow through the economy via investments in large-scale public works.

[? if in_popular_front or in_left_front: The KPD opposes the WTB plan, seeing it as a rescue of capitalism rather than a step towards real socialism. ?]

@implement_wtb_deficit
title: Implement the WTB plan.
subtitle: -[+ wtb_budget +] budget - we will enter deficit spending. [? if wtb_budget == 3 : Our pro-labor policies have reduced the cost of the plan. ?]
unavailable-subtitle: [? if unemployed <= 8 : Unemployment is low enough that the time for the WTB-Plan has passed. ?][? if economic_minister_party == "DVP" : The DVP-controlled Economic Ministry is obstructing our plans. ?][? if finance_minister_party != "SPD" : Without control over finances, we cannot undertake such deficit spending. ?]
view-if: budget < wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8 and economic_minister_party != "DVP" and finance_minister_party == "SPD"
on-arrival: wtb_deficit_vote = 1
go-to: wtb_deficit_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to implement the WTB plan, funded through deficit spending.

@wtb_deficit_done
on-arrival: coalition_dissent += 1; coalition_dissent += 1 if historical_mode; coalition_dissent += 1 if chancellor == "BrÃ¼ning"; works_program += 1; budget -= wtb_budget; unemployed -= 4; workers += 4; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; economic_growth += 3; coalition_dissent += 1 if in_grand_coalition; pro_republic += 3*(1-dissent); socialism += 3
achievement: wirtschaftspolitik

We have entered the realm of deficit spending. Our coalition partners are disgusted, but the works plan is enacted anyway. Money is beginning to flow through the economy via investments in large-scale public works.

[? if in_popular_front or in_left_front: The KPD opposes the WTB plan, seeing it as a rescue of capitalism rather than a step towards real socialism. ?]


####################### left economic policies (socializations)

@nationalize_1
title: Implement the Left plan for the transformation of the economy.
subtitle: -[+ nationalize_budget +] budget. [? if works_councils >= 3 : Our works councils have reduced the budget necessary for this program. ?] [? if socializations >= 2: Our existing socialization policies have reduced the budget necessary for this program. ?]
view-if: nationalization_adopted == 1 and nationalization_progress == 0
choose-if: economic_minister_party != "DVP"
unavailable-subtitle: The DVP-controlled Economic Ministry could never allow such an action.
on-arrival: nationalize_vote = 1
go-to: nationalize_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to nationalize key and idle industries.

@nationalize_done
on-arrival: socialism += 4 if nationalization_progress < 1
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget


@nationalize_no_deficit
on-arrival: nationalization_progress += 1; capital_strike_progress += 2; workers += 3.5 if unemployed >= 6; unemployed -= 3.5 if unemployed >= 6; workers += 1.2 if works_councils >= 3; unemployed -= 1.2 if works_councils >= 3; unemployed -= 2 if nationalization_progress < 2; workers += 2 if nationalization_progress < 2; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 1; coalition_dissent += 1 if in_grand_coalition; dvp_relation -= 4; left_dissent -= 5; new_middle_spd -= 1; new_middle_spd += 3 if new_middle_conscious; old_middle_spd -= 2; economy_goal_completed += 1; inflation += 2; economic_growth += 1.7 if economic_growth < 5
achievement: wirtschaftspolitik

[? if nationalization_progress == 1 : We have begun purchasing idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress == 1 : The Left argues that this should only be a first step in the socialist transformation of the economy. Eventually, much of the economy must be brought under popular control. ?]

[? if nationalization_progress > 1 : We purchase additional underutilized enterprises and run them in a coordinated socialist fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@nationalize_deficit
on-arrival: socialism += 1

Unfortunately, we do not have the budget to begin buying out industrial facilities. Some are urging for deficit spending in order to purchase factories, while others are encouraging (currently illegal) factory takeovers by workers, or nationalization without compensation. Taking any of these actions would require a vast departure from our previous party stances.

- @nationalize_deficit_1
- @nationalize_without_compensation
- @empower_workers
- @return: No, we should not do this.


@nationalize_deficit_1
title: Take a budget deficit to fund the nationalization program.
choose-if: (left_strength > reformist_strength + center_strength or woytinsky_advisor == 1) and (finance_minister_party == "SPD" or finance_minister_party == "KPD")
subtitle: [? if woytinsky_advisor: Woytinsky can bring us around to supporting a budget deficit. ?]
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: The party does not yet support budget deficits for this purpose. ?] [? if not woytinsky_advisor : Perhaps bringing on an advisor might help make this action palatable? ?][? if finance_minister_party != "SPD" and finance_minister_party != "KPD" : Without control over finances, we cannot undertake such deficit spending. ?]
on-arrival: nationalization_progress += 1; capital_strike_progress += 3; workers += 3 if unemployed >= 6; workers += 1.2 if works_councils >= 3; unemployed -= 3 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 2; coalition_dissent += 1 if in_grand_coalition; dvp_relation -= 7; z_relation -= 4; new_middle_spd -= 2; new_middle_spd += 2 if new_middle_conscious; old_middle_spd -= 3; economy_goal_completed += 1; inflation += 3; economic_growth += 2 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if woytinsky_advisor: Most of the party, even on the left, are opposed to budget deficits for any purpose, with the exception of Wladimir Woytinsky. While he did not originally support the left's "transformational" plan, he has come around, and has persuaded the party and the Finance Ministry to support budget deficits in limited cases, for the purpose of exiting from the depression. ?]

[? if nationalization_progress == 1 : We have begun purchasing idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress == 1 : The Left argues that this should only be a first step in the socialist transformation of the economy. Eventually, much of the economy must be brought under popular control. ?]

[? if nationalization_progress > 1 : We purchase additional underutilized enterprises and run them in a coordinated socialist fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@nationalize_without_compensation
title: Nationalize without compensation.
subtitle: There is no reason why we should not simply take over idle factories. This is likely to provoke a confrontation with the capitalists.
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: The bulk of the party does not support this quasi-legal action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if left_coalition < 50 : We lack a majority in the Reichstag. ?]
choose-if: left_strength > reformist_strength + center_strength and judicial_reform >= 3 and left_coalition >= 50
on-arrival: nationalization_progress += 1; capital_strike_progress += 7; coup_progress += 3; workers += 2.5 if unemployed >= 6; workers += 1.2 if works_councils >= 3 and unemployed >= 3; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3 and unemployed >= 3; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 6*(1-dissent); coalition_dissent += 2; coalition_dissent += 1 if in_grand_coalition; coalition_dissent += 1 if capital_strike_progress >= 10; dvp_relation -= 12; z_relation -= 8; ddp_relation -= 8; left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 3; new_middle_spd += 1 if new_middle_conscious; old_middle_spd -= 4; economy_goal_completed += 1; kpd_relation += 5*(1-dissent); inflation += 4; economic_growth += 1 if economic_growth < 4; socialism -= 1
achievement: wirtschaftspolitik

The capitalists are, predictably, up in arms about our policy of nationalization without compensation. They call it illegal expropriation, and it is. Nevertheless, it is clear that they have failed by keeping their factories idle, and we must take radical action.

[? if nationalization_progress == 1 : We have begun acquiring idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress > 1 : We acquired additional underutilized enterprises and run them in a coordinated socialist fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@empower_workers
title: Empower workers to take over factories.
subtitle: We can promise to legalize factory occupations, giving the factories over to worker unions. This is highly confrontational, and would likely provoke a reaction from right-wing militants as well as capitalists.
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : The bulk of the party does not support this radical action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if rb_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if works_councils < 1 : We need the support of the works councils first. ?][? if left_coalition < 50 : We lack a majority in the Reichstag. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1 and left_coalition >= 50
on-arrival: nationalization_progress += 1; capital_strike_progress += 6; coup_progress += 5; workers += 3 if unemployed >= 6; workers += 1.5 if works_councils >= 3 and unemployed > 4; unemployed -= 3 if unemployed >= 6; unemployed -= 1.5 if works_councils >= 3 and unemployed > 4; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; coalition_dissent += 1 if in_grand_coalition; coalition_dissent += 1 if capital_strike_progress >= 10; coalition_dissent += 1 if coup_progress >= 8; dvp_relation -= 12; z_relation -= 8; ddp_relation -= 8; workers_nsdap -= 5*(1-dissent); unemployed_nsdap -= 5*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 3; new_middle_spd += 2 if new_middle_conscious; old_middle_spd -= 4; factory_takeovers += 1; economy_goal_completed += 1; kpd_relation += 5*(1-dissent); inflation += 3; economic_growth += 1 if economic_growth < 3; socialism += 1
achievement: wirtschaftspolitik

The capitalists are, predictably, up in arms about our policy of legalizing factory occupations. They call it illegal expropriation, and it is. Nevertheless, it is clear that they have failed, and we must take radical action.

Fascists have been taking increasingly violent actions around the occupied factories, calling us thieves and looters. However, the some of the working-class supporters of the fascists have been peeled off, or at least the ones with a sense of class solidarity.


@nationalize_2
title: Continue the Left plan for the transformation of the economy.
subtitle: -[+ nationalize_budget +] budget. [? if works_councils >= 3 : Our works councils have reduced the budget necessary for this program. ?] [? if socializations + nationalization_progress >= 2: Our existing socialization policies have reduced the budget necessary for this program. ?]
view-if: nationalization_adopted == 1 and nationalization_progress >= 1
choose-if: economic_minister_party != "DVP"
unavailable-subtitle: The DVP-controlled Economic Ministry could never allow such an action.
on-arrival: nationalize_vote = 1
go-to: nationalize_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to nationalize key and idle industries.

#######################################
# Moderate economic plan

@moderate_plan
title: Implement the Reformists' job-creation plan.
subtitle: -2 budget.
unavailable-subtitle: -2 budget. [? if budget < 2 : We do not have the necessary budget. ?] [? if unemployed < 8 : Unemployment is low enough that job creation is not necessary. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 2 and unemployed >= 8
on-arrival: reformist_vote = 1; reformist_budget = 2
go-to: reformist_done_1 if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to begin a public works program.

@reformist_done_1
on-arrival: budget -= 2; works_program += 1; coalition_dissent += 1 if dvp_relation < 35 and in_grand_coalition; workers += 3 if unemployed >= 8; unemployed -= 3 if unemployed >= 8; inflation += 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 4*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; economic_growth += 1.6; pro_republic += 3*(1-dissent); socialism -= 1
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : We have started to implement the job-creation plan! Funds are sent to build public works projects across Germany, reducing unemployment. ?]

[? if moderate_plan_progress > 1 : We have continued to fund the job-creation plan, using our funds to build public works throughout Germany. ?]

[? if unemployed >= 10 : The bourgeois approve of this plan as the "least bad" alternative, seeing it as necessary to save Germany's economy[? if dvp_relation < 35 and in_grand_coalition : though the DVP is still somewhat opposed. ?]. ?]

[? if unemployed < 10 : Unemployment is low enough that the plan does not seem to have the intended effect. ?]

[? if in_popular_front or in_left_front: The KPD opposes the economic plan, seeing it as a rescue of capitalism rather than a step towards real socialism. ?]

@moderate_plan_limited_budget
title: Partially implement the Reformists' job-creation plan, with a lower cost.
subtitle: -1 budget.
unavailable-subtitle: -1 budget. [? if budget < 1 : We do not have the necessary budget. ?] [? if unemployed < 8 : Unemployment is low enough that job creation is not necessary. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 1 and unemployed >= 8
on-arrival: reformist_vote = 1; reformist_budget = 1
go-to: reformist_done_2 if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to begin a public works program.

@reformist_done_2
on-arrival: budget -= 1; works_program += 1; workers += 2 if unemployed >= 8; unemployed -= 2 if unemployed >= 8; inflation += 1; workers_spd += 4*(1-dissent); unemployed_spd += 2*(1-dissent); new_middle_spd += 2*(1-dissent); old_middle_spd += 1*(1-dissent); rural_spd += 1*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; economic_growth += 0.7; pro_republic += 3*(1-dissent);
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : We have started to implement the job-creation plan! Funds are sent to build public works projects across Germany, reducing unemployment. ?]

[? if moderate_plan_progress > 1 : We have continued to fund the job-creation plan, using our funds to build public works throughout Germany. ?]

[? if unemployed >= 10 : The bourgeois approve of this plan as the "least bad" alternative, seeing it as necessary to save Germany's economy. ?]

[? if unemployed < 10 : Unemployment is low enough that the plan does not seem to have the intended effect. ?]

[? if in_popular_front or in_left_front: The KPD opposes the economic plan, seeing it as a rescue of capitalism rather than a step towards real socialism. ?]

#######################################
# Synthesis plan

@synthesis_plan
title: Implement the Centrist job-creation and nationalization plan.
subtitle: -[+ synthesis_budget +] budget. [? if synthesis_budget < 3 : Our existing programs makes this cheaper. ?]
unavailable-subtitle: [? if budget < synthesis_budget : -[+ synthesis_budget +] budget. We do not have the necessary budget. ?][? if economic_minister_party == "DVP" : The DVP-controlled Economic Ministry is obstructing our plans. ?]
view-if: synthesis_plan_adopted == 1
choose-if: budget >= synthesis_budget and economic_minister_party != "DVP"
on-arrival: synthesis_vote = 1
go-to: synthesis_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to begin a public works program coupled with nationalizations.

@synthesis_done
on-arrival: budget -= synthesis_budget; synthesis_progress += 1; works_program += 1; workers += 2 if unemployed >= 8; unemployed -= 2 if unemployed >= 8; inflation += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); new_middle_spd += 2*(1-dissent); new_middle_spd -= 1*(1-dissent) if not new_middle_conscious; rural_spd += 1*(1-dissent); old_middle_spd -= 1; moderate_plan_progress += 1; inflation += 1 if unemployed < 10; economic_growth += 0.7; pro_republic += 3*(1-dissent); nationalization_progress += 1; capital_strike_progress += 1; unemployed -= 1.5 if unemployed >= 6; workers += 0.5 if works_councils >= 3; unemployed -= 0.5 if works_councils >= 3; workers += 1.5 if unemployed >= 6; workers_spd += 5*(1-dissent); unemployed_spd += 3*(1-dissent); coalition_dissent += 1 if in_grand_coalition; dvp_relation -= 3; left_dissent -= 3; economy_goal_completed += 1; inflation += 1; economic_growth += 0.8 if economic_growth < 5
achievement: wirtschaftspolitik

A simultaneous public works and nationalization program begins, intended to both begin progress towards dismantling capitalism and alleviate the immediate effects of the crisis on the German people.

[? if nationalization_progress == 1 : The Left argues that this should only be a first step in the socialist transformation of the economy. Eventually, much of the economy must be brought under popular control. ?]

@synthesis_only_works
title: Implement the Centrist public works program without nationalizations.
subtitle: -1 budget.
unavailable-subtitle: -1 budget. [? if budget < 1 : We do not have the necessary budget. ?]
view-if: synthesis_plan_adopted
choose-if: budget >= 1
on-arrival: reformist_vote = 1; reformist_budget = 1
go-to: reformist_done_2 if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to begin a public works program.
